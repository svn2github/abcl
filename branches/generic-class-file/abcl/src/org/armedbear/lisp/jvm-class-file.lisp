;;; jvm-class-file.lisp
;;;
;;; Copyright (C) 2010 Erik Huelsmann
;;; $Id$
;;;
;;; This program is free software; you can redistribute it and/or
;;; modify it under the terms of the GNU General Public License
;;; as published by the Free Software Foundation; either version 2
;;; of the License, or (at your option) any later version.
;;;
;;; This program is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU General Public License for more details.
;;;
;;; You should have received a copy of the GNU General Public License
;;; along with this program; if not, write to the Free Software
;;; Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
;;;
;;; As a special exception, the copyright holders of this library give you
;;; permission to link this library with independent modules to produce an
;;; executable, regardless of the license terms of these independent
;;; modules, and to copy and distribute the resulting executable under
;;; terms of your choice, provided that you also meet, for each linked
;;; independent module, the terms and conditions of the license of that
;;; module.  An independent module is a module which is not derived from
;;; or based on this library.  If you modify this library, you may extend
;;; this exception to your version of the library, but you are not
;;; obligated to do so.  If you do not wish to do so, delete this
;;; exception statement from your version.

(in-package "JVM")

#|

The general design of the class-file writer is to have generic
- human readable - representations of the class being generated
during the construction and manipulation phases.

After completing the creation/manipulation of the class, all its
components will be finalized. This process translates readable
(e.g. string) representations to indices to be stored on disc.

The only thing to be done after finalization is sending the
output to a stream ("writing").


Finalization happens highest-level first. As an example, take a
method with exception handlers. The exception handlers are stored
as attributes in the class file structure. They are children of the
method's Code attribute. In this example, the body of the Code
attribute (the higher level) gets finalized before the attributes.
The reason to do so is that the exceptions need to refer to labels
(offsets) in the Code segment.


|#


(defun map-primitive-type (type)
  "Maps a symbolic primitive type name to its Java string representation."
  (case type
    (:int        "I")
    (:long       "J")
    (:float      "F")
    (:double     "D")
    (:boolean    "Z")
    (:char       "C")
    (:byte       "B")
    (:short      "S")
    ((nil :void) "V")))


#|

The `class-name' facility helps to abstract from "this instruction takes
a reference" and "this instruction takes a class name". We simply pass
the class name around and the instructions themselves know which
representation to use.

|#

(defstruct (class-name (:conc-name class-)
                       (:constructor %make-class-name))
  name-internal
  ref
  array-ref)

(defun make-class-name (name)
  "Creates a `class-name' structure for the class or interface `name'.

`name' should be specified using Java representation, which is converted
to 'internal' (JVM) representation by this function."
  (setf name (substitute #\/ #\. name))
  (%make-class-name :name-internal name
                    :ref (concatenate 'string "L" name ";")
                    :array-ref (concatenate 'string "[L" name ";")))

(defmacro define-class-name (symbol java-dotted-name &optional documentation)
  "Convenience macro to define constants for `class-name' structures,
initialized from the `java-dotted-name'."
  `(defconstant ,symbol (make-class-name ,java-dotted-name)
     ,documentation))

(define-class-name +java-object+ "java.lang.Object")
(define-class-name +java-string+ "java.lang.String")
(define-class-name +lisp-object+ "org.armedbear.lisp.LispObject")
(define-class-name +lisp-simple-string+ "org.armedbear.lisp.SimpleString")
(define-class-name +lisp+ "org.armedbear.lisp.Lisp")
(define-class-name +lisp-nil+ "org.armedbear.lisp.Nil")
(define-class-name +lisp-class+ "org.armedbear.lisp.LispClass")
(define-class-name +lisp-symbol+ "org.armedbear.lisp.Symbol")
(define-class-name +lisp-thread+ "org.armedbear.lisp.LispThread")
(define-class-name +lisp-closure-binding+ "org.armedbear.lisp.ClosureBinding")
(define-class-name +lisp-integer+ "org.armedbear.lisp.LispInteger")
(define-class-name +!lisp-fixnum+ "org.armedbear.lisp.Fixnum")
(define-class-name +!lisp-bignum+ "org.armedbear.lisp.Bignum")
(define-class-name +!lisp-single-float+ "org.armedbear.lisp.SingleFloat")
(define-class-name +!lisp-double-float+ "org.armedbear.lisp.DoubleFloat")
(define-class-name +lisp-cons+ "org.armedbear.lisp.Cons")
(define-class-name +lisp-load+ "org.armedbear.lisp.Load")
(define-class-name +lisp-character+ "org.armedbear.lisp.LispCharacter")
(define-class-name +lisp-structure-object+ "org.armedbear.lisp.StructureObject")
(define-class-name +lisp-simple-vector+ "org.armedbear.lisp.SimpleVector")
(define-class-name +lisp-abstract-string+ "org.armedbear.lisp.AbstractString")
(define-class-name +lisp-abstract-vector+ "org.armedbear.lisp.AbstractVector")
(define-class-name +lisp-abstract-bit-vector+
    "org.armedbear.lisp.AbstractBitVector")
(define-class-name +lisp-environment+ "org.armedbear.lisp.Environment")
(define-class-name +lisp-special-binding+ "org.armedbear.lisp.SpecialBinding")
(define-class-name +lisp-special-bindings-mark+
    "org.armedbear.lisp.SpecialBindingsMark")
(define-class-name +lisp-throw+ "org.armedbear.lisp.Throw")
(define-class-name +lisp-return+ "org.armedbear.lisp.Return")
(define-class-name +lisp-go+ "org.armedbear.lisp.Go")
(define-class-name +lisp-primitive+ "org.armedbear.lisp.Primitive")
(define-class-name +lisp-eql-hash-table+ "org.armedbear.lisp.EqlHashTable")
(define-class-name +lisp-hash-table+ "org.armedbear.lisp.HashTable")
(define-class-name +lisp-package+ "org.armedbear.lisp.Package")
(define-class-name +lisp-readtable+ "org.armedbear.lisp.Readtable")
(define-class-name +lisp-stream+ "org.armedbear.lisp.Stream")
(define-class-name +lisp-closure+ "org.armedbear.lisp.Closure")
(define-class-name +lisp-compiled-closure+ "org.armedbear.lisp.CompiledClosure")
(define-class-name +lisp-closure-parameter+
    "org.armedbear.lisp.Closure$Parameter")
(define-class-name +!fasl-loader+ "org.armedbear.lisp.FaslClassLoader")

#|

Lisp-side descriptor representation:

 - list: a list starting with a method return value, followed by
     the argument types
 - keyword: the primitive type associated with that keyword
 - class-name structure instance: the class-ref value

The latter two can be converted to a Java representation using
the `internal-field-ref' function, the former is to be fed to
`descriptor'.

|#

(defun internal-field-type (field-type)
  "Returns a string containing the JVM-internal representation
of `field-type', which should either be a symbol identifying a primitive
type, or a `class-name' structure identifying a class or interface."
  (if (symbolp field-type)
      (map-primitive-type field-type)
      (class-name-internal field-type)))

(defun internal-field-ref (field-type)
  "Returns a string containing the JVM-internal representation of a reference
to `field-type', which should either be a symbol identifying a primitive
type, or a `class-name' structure identifying a class or interface."
  (if (symbolp field-type)
      (map-primitive-type field-type)
      (class-ref field-type)))

(defun descriptor (return-type &rest argument-types)
  "Returns a string describing the `return-type' and `argument-types'
in JVM-internal representation."
  (format nil "(窿立磲疸狎＇轭翦蝾犰骈屐洵蝈狎珲礤铘豉疱螬ㄩ铘弪钺飙骈屐洵蝈蝈趱蝾豉疱┅ㄤ彐篝蝓泗痫镬换嚅钿屮泔铘衢铙翳轭溴镦翳灬篝犰祜汜翦箪雉ò浇屙痿换⒘泔铙翎铘痫镬孱趄轶泔铙殇弪邃鲠扉殒轸栳换犷轭溴珧遽翦翳犷弪铹犷戾篌翳犷痫镬泔躅簪ㄩ钿屮癌孱趄殄蟓扉篝换翳孱趄殄栳箬篝矧弩蜥鲠祯弩屮沐痿轭汜箦镦篝蜷铉犷换豸娓忮汜躞怙翳狎篝蜷铉鲠祯弩ㄥ铘蜷弩磲脲栳箬翎忪呼弩＇羼踽后辁舶锤候彖狍璀箝伯癌┅ㄤ彐篝蝓泗泔铙翎铘⒂趄蹉趱蝈麸忮轭沆蹁邃轭犰泔铙翎铘篚猸豉疱螽翎轭溴ㄤ彐疳蜥礤翦泔铙翎铘豉疱磲皤Жê沆狍暴ê骈屐洵蝈暴ê礤翳镤蝈卑暴换ê轭翦蜴徙瀛礤翳镤蝈北ê篝蜷铉暴ê轭翦珏暴ê骒镝暴ê祜铉博ê滹踱戾博ê钺礤犷洵豉疱辈暴ê豸娓暴┅ㄤ彐篝蝓泗ㄣ镱篝犷舡沆狍ê泔铙趄蹉麸磲脲泔铙翎铘沆狍ㄩ钿屮钺礤轭溴┅ê轭沆蹁泔铙翎铘翎珐┅钺礤轭溴ㄤ彐篝蝓泗ㄣ镱篝犷舡礤礅弪蝈ê泔铙趄蹉麸ロ犭瀛泔铙翎铘礤礅弪蝈翎轭溴沆狍蟓轭溴钺礤豉疱轭溴┅ê轭沆蹁泔铙翎铘┅沆狍蟓轭溴钺礤豉疱轭溴ㄤ邈灬轫ㄩ铎轭磲脲泔铙翎铘骈屐洵蝈磲脲泔铙翎铘礤翳镤蝈磲脲泔铙翎铘轭翦蜴徙瀛礤翳镤蝈姗ㄤ彐躅磲脲泔铙翎铘骈屐洵蝈ㄩ钿屮沆狍蟓轭溴钺礤豉疱轭溴⒚蝈狒弩嚆镱篝犷舡礤礅弪蝈妲轭篝犷沐泔铘衢铋铉骈屐蝈驽蝈钽瀹ē磲脲泔铙翎铘礤礅弪蝈轭溴沆狍蟓轭溴钺礤豉疱轭溴┅ㄤ彐躅磲脲泔铙翎铘礤翳镤蝈ㄩ钿屮沆狍蟓轭溴钺礤豉疱轭溴⒚蝈狒弩嚆镱篝犷舡礤礅弪蝈妲轭篝犷沐泔铘衢铋铉礤翳镤蝈驽蝈钽瀹ē磲脲泔铙翎铘礤礅弪蝈卑轭溴沆狍蟓轭溴钺礤豉疱轭溴┅ㄤ彐躅磲脲泔铙翎铘轭翦蜴徙瀛礤翳镤蝈ㄩ钿屮沆狍蟓轭溴钺礤豉疱轭溴⒚蝈狒弩嚆镱篝犷舡礤礅弪蝈妲轭篝犷沐泔铘衢铋铉犷轭翦蜴徙瀛礤翳镤蝈驽蝈钽瀹ē磲脲泔铙翎铘礤礅弪蝈北轭溴沆狍蟓轭溴钺礤豉疱轭溴┅ㄤ彐篝蝓泗ㄣ镱篝犷舡篝蜷铉ê泔铙趄蹉麸磲脲泔铙翎铘篝蜷铉ㄩ钿屮鲠祯瀛轭溴┅ê轭沆蹁泔铙翎铘翎俯┅鲠祯瀛轭溴ㄤ彐篝蝓泗ㄣ镱篝犷舡骒镝舣轭ê泔铙趄蹉麸ロ犭瀛泔铙翎铘骒镝舣轭翎轭溴鲠祯濠ê轭沆蹁泔铙翎铘┅鲠祯濠ㄤ邈灬轫ㄩ铎轭磲脲泔铙翎铘骒镝磲脲泔铙翎铘轭舂ㄤ彐躅磲脲泔铙翎铘骒镝ㄩ钿屮鲠祯濠⒚蝈狒弩嚆镱篝犷舡骒镝舣轭臾篝蝓泗躜轭篝犷沐泔铘衢铋铉骒镝舢ē磲脲泔铙翎铘骒镝舣轭轭溴鲠祯濠ㄤ彐躅磲脲泔铙翎铘轭ㄩ钿屮鲠祯濠⒚蝈狒弩嚆镱篝犷舡骒镝舣轭臾篝蝓泗躜轭篝犷沐泔铘衢铋铉犷轭舢ē磲脲泔铙翎铘骒镝舣轭轭溴鲠祯濠ㄤ彐篝蝓泗ㄣ镱篝犷舡滹踱戾祜铉ê泔铙趄蹉麸ロ犭瀛泔铙翎铘滹踱戾祜铉翎轭溴鲠祯濠ê轭沆蹁泔铙翎铘┅鲠祯濠ㄤ邈灬轫ㄩ铎轭磲脲泔铙翎铘滹踱戾磲脲泔铙翎铘骒镝舂ㄤ彐躅磲脲泔铙翎铘滹踱戾ㄩ钿屮鲠祯濠⒚蝈狒弩嚆镱篝犷舡滹踱戾祜铉篝蝓泗躜轭篝犷沐泔铘衢铋铉滹踱戾ē磲脲泔铙翎铘滹踱戾祜铉轭溴鲠祯濠ㄤ彐躅磲脲泔铙翎铘祜铉ㄩ钿屮鲠祯濠⒚蝈狒弩嚆镱篝犷舡滹踱戾祜铉篝蝓泗躜轭篝犷沐泔铘衢铋铉祜铉ē磲脲泔铙翎铘滹踱戾祜铉轭溴鲠祯濠ㄤ彐篝蝓泗ㄣ镱篝犷舡钺礤豉疱ê泔铙趄蹉麸磲脲泔铙翎铘钺礤豉疱ㄩ钿屮钺礤轭溴溴筱蜷痿矧轭溴┅ê轭沆蹁泔铙翎铘翎辈┅钺礤轭溴溴筱蜷痿矧轭溴ㄤ彐篝蝓泗ㄣ镱篝犷舡豸娓ê泔铙趄蹉麸磲脲泔铙翎铘豸娓ㄩ钿屮鲠祯濠ê轭沆蹁泔铙翎铘翎暴┅鲠祯濠ㄤ彐躅痫镬徜洵沆狍痫镬沆狍螬⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬沆狍轸屙骘嚆灬篌М嚆灬篌眭篝忮犷轭篝犷沐镦嚆灬篌钺礤М戾è孱趄ㄧ弭栳箬沆狍痫镬孱趄殄痫镬┅┅躅戾篌孱趄戾è豸娓痫镬徜洵豸娓痫镬ㄣ灬篌钺礤轭翦蝾犰沆狍螬┅箦翩孱趄磲脲泔铙翎铘沆狍ㄩ钽痫镬轭溴痫镬┅豸娓ㄧ弭栳箬沆狍痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵骈屐洵蝈痫镬沆狍钺礤豉疱⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙麒殂溴铒翦蝈驽蝈钽麸翳囝犴濮骈屐镦翳嚆灬篌К忮轭镦圄疱М嚆灬篌箬秕熹忮犷轭篝犷沐镦嚆灬篌钺礤М囝犴濮轶篝蜷铉圄疱轶骈屐洵豉疱箦嚅铘弪钺飙骈屐洵豉疱З戾è孱趄ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅┅躅戾篌孱趄戾è痫镬徜洵沆狍痫镬沆狍螬畀痫镬徜洵钺礤豉疱痫镬钺礤豉疱┅箦翩孱趄磲脲泔铙翎铘骈屐洵蝈ㄩ钽痫镬轭溴痫镬┅畀舂ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵礤翳镤蝈痫镬沆狍钺礤豉疱⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙麒殂溴铒翦蝈驽蝈钽麸翳礤翳镤鏖翳囝犴濮轭嚆灬篌К麒殂轶镦圄疱М儒蝈圄疱轶礤翳镤溴筱蜷痿矧麒殂溴骈铄翳狎珲礤铘豉疱犷蝈趱蝾豉疱嚆灬篌轶犷轭篝犷沐镦嚆灬篌钺礤М戾è孱趄ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅┅躅戾篌孱趄戾è痫镬徜洵沆狍痫镬沆狍螬畀痫镬徜洵钺礤豉疱痫镬钺礤豉疱┅箦翩孱趄磲脲泔铙翎铘礤翳镤蝈ㄩ钽痫镬轭溴痫镬┅畀舂ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵轭翦蜴徙瀛礤翳镤蝈痫镬沆狍钺礤豉疱⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙麒殂溴铒翦蝈驽蝈钽麸翳礤翳镤囝犴濮轭翳轭翦蜴徙嚆灬篌К麒殂轶镦圄疱М渝囵镲飙徜洵礤翳镤蝈妲骘蝈磲螂螽戾è孱趄ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅┅躅戾篌孱趄戾è痫镬徜洵沆狍痫镬沆狍螬畀痫镬徜洵钺礤豉疱痫镬钺礤豉疱┅箦翩孱趄磲脲泔铙翎铘轭翦蜴徙瀛礤翳镤蝈ㄩ钽痫镬轭溴痫镬┅畀舂ㄧ弭栳箬ㄡ泔铙钺礤豉疱沆狍螬痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵篝蜷铉痫镬篝蜷铉⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳篝蜷铉戾è孱趄ㄧ弭栳箬ㄣ镱篝蜷铉换浇篝蜷铉翎痫镬孱趄殄痫镬┅┅躅戾篌孱趄戾è豸娓痫镬徜洵豸娓痫镬篝蜷铉┅箦翩孱趄磲脲泔铙翎铘篝蜷铉ㄩ钽痫镬轭溴痫镬┅豸娓ㄧ弭栳箬ㄣ镱篝蜷铉痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵轭痫镬轭舂⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳轭舢戾è孱趄ㄧ弭栳箬ㄣ镱轭舂痫镬孱趄殄痫镬┅┅躅戾篌孱趄箦翩孱趄磲脲泔铙翎铘轭ㄩ钽痫镬轭溴痫镬┅轭舂ㄧ弭栳箬ㄣ镱轭舂痫镬孱趄殄痫镬┅孱趄瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵骒镝痫镬骒镝舂⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳骒镝舢戾è孱趄ㄧ弭栳箬ㄣ镱骒镝舂痫镬孱趄殄痫镬┅┅躅戾篌孱趄箦翩孱趄磲脲泔铙翎铘骒镝ㄩ钽痫镬轭溴痫镬┅骒镝舂ㄧ弭栳箬ㄣ镱骒镝舂痫镬孱趄殄痫镬┅孱趄瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵祜铉痫镬祜铉⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳祜铉戾è孱趄ㄧ弭栳箬ㄣ镱祜铉痫镬孱趄殄痫镬┅┅躅戾篌孱趄箦翩孱趄磲脲泔铙翎铘祜铉ㄩ钽痫镬轭溴痫镬┅祜铉ㄧ弭栳箬ㄣ镱祜铉痫镬孱趄殄痫镬┅孱趄瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄩ钽痫镬轭溴痫镬┅换滹踱戾轭溴轭泸遽箦祜铉翎脲箪雉ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵滹踱戾痫镬滹踱戾⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳滹踱戾戾è孱趄ㄧ弭栳箬ㄣ镱滹踱戾痫镬孱趄殄痫镬┅┅躅戾篌孱趄箦翩孱趄磲脲泔铙翎铘滹踱戾ㄩ钽痫镬轭溴痫镬┅滹踱戾ㄧ弭栳箬ㄣ镱滹踱戾痫镬孱趄殄痫镬┅孱趄瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄩ钽痫镬轭溴痫镬┅换滹踱戾轭溴轭泸遽箦т秕忪濮翎脲箪雉ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵钺礤豉疱痫镬钺礤豉疱⒁弭躜铙翳轭溴镦翳泔铙翎铘痫镬轸屙溴铒糸铉翳钺礤豉疱殇孱糸骈弪戾è孱趄ㄧ弭栳箬ㄣ镱钺礤豉疱痫镬孱趄殄痫镬┅ㄩ铘弪钺飙豉疱ㄩ扉篝豉疱ㄡ痧禊＇溴筱蜷痿矧豉疱ㄩ铘弪钺飙骈屐洵蝈豉疱┅┅躅戾篌孱趄戾è痫镬徜洵豸娓痫镬钺礤┅ㄩ痫镬徜洵豸娓痫镬轭翦蝾犰豉疱┅箦翩孱趄磲脲泔铙翎铘钺礤豉疱ㄩ钽痫镬轭溴痫镬┅榄舂ㄧ弭栳箬ㄣ镱钺礤豉疱痫镬孱趄殄痫镬┅孱趄┅瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐躅痫镬徜洵豸娓痫镬豸娓狍篝蜷铉⒁弭躜铙翳轭溴镦翳翦趱犰鲠祯翳狒鏖祆忮篝矧邃轭翳沆狍骈戾狍赵骗孱泔溴溽翎戾è孱趄ㄧ弭栳箬ㄣ镱北豸娓狍篝蜷铉换北浇豸娓痫镬孱趄殄痫镬┅┅躅戾篌孱趄箦翩孱趄磲脲泔铙翎铘豸娓ㄩ钽痫镬轭溴痫镬┅豸娓狍篝蜷铉ㄧ弭栳箬ㄣ镱北豸娓狍篝蜷铉痫镬孱趄殄痫镬┅孱趄瘐箬孱趄痫镬孱趄殄蟓扉篝痫镬┅ㄣ镱篝犷舡轭溴孱趄┅ㄤ彐篝蝓泗ㄣ灬篌骈戾ê泔铙趄蹉麸№犭瀛沆狍蟓骈戾ㄣ灬篌篚疱蜚灬篌徙沐篌骒徵螬┅ㄣ镱篝犷趔磲脲痫镬┅徙沐篌骒徵沆狍篚疱蜚灬篌换轭翦蜴徙弩骈屐潴礤翳镤狒趄殁豸弩ㄤ彐躅沆狍蟓徜洵骈屐ㄣ灬篌骈屐洎⒘滗噫殄熹泸遽翦怡囗犭瀛骈屐洄瘐箬骈屐ㄣ灬篌骈戾骈屐潴沆狍螬┅ㄤ彐躅沆狍蟓骈屐ㄣ灬篌钺礤⑵轭潴骈屐怡钺礤换＃篝蜷泗禊箴遽腴铉骈屐轶躅轳蹂禊换殇孱糸骈邃怡轸钺礤犷豉疱铒怡翳钺礤犰镱瀹ㄦ轭钺礤ㄣ灬篌骈戾骈屐潴沆狍螬呼弩＇篝蜷铉弘妁＇骈屐洵钺礤┅ㄤ彐躅沆狍蟓徜洵礤翳镤ㄣ灬篌礤翳镤⒘滗囗弭栾洄麸嚆灬篌Щ翳礤翳镤眭篝栳鲥忮孱泸遽翦躞轭囗犭瀛礤翳镤М瘐箬礤翳镤ㄣ灬篌骈戾礤翳镤沆狍螬┅ㄤ彐躅沆狍蟓礤翳镤蟓怡钺礤ㄣ灬篌钺礤⒁弭躜铙犰礤翳镤麒殂栳鲥囝犴濮蝈盹鲥钺礤ㄣ灬篌骈戾礤翳镤沆狍螬呼弩舡铒＇篝蜷铉弘妁＇礤翳镤钺礤┅ㄤ彐躅沆狍蟓礤翳镤ㄣ灬篌钺礤蝈趱蝾蝈篝狎珞⒁弭躜翳礤翳镤麒殂轶躅轳蹂禊殇孱糸骈邃怡轸钺礤廖溴筱蜷痿矧戾è蝈趱蝾犷洵狎珞ㄣ镱蝈趱蝾狎珞┅ㄦ轭洵殒＇灬礅溽ㄣㄡ钿篝蜷铉礤翳镤钺礤悌钺礤ㄥ聃犰礤翳镤溴筱蜷痿矧悌蝈趱蝾犷洵狎珞┅ㄣ灬篌骈戾礤翳镤沆狍螬┅ㄤ彐躅沆狍蟓徜洵狒趄殁豸ㄣ灬篌狒趄殁豸濠⒘滗噌趑蜷怩翦麸翳沆狍蠡狒趄殁豸弩眭篝忮轭篝犷沐镦篝蝓泗躜沆狍箦麒殂轭沆蹁翳噌趑蜷怩翦篝蝓泗躜沆狍螽瘐箬狒趄殁豸ㄣ灬篌骈戾狒趄殁豸弩沆狍螬┅ㄤ彐躅沆狍蟓狒趄殁豸ㄣ灬篌钺礤⒁弭躜铙翳狒趄殁豸麒殂轶钺礤囝犴濮ㄦ轭钺礤ㄣ灬篌骈戾狒趄殁豸弩沆狍螬呼弩＇篝蜷铉弘妁＇狒趄殁豸瀛钺礤┅ㄤ彐躅骈钺扉瀛沆狍蟓骈戾ㄣ灬篌⒃蜥铙骘蝽翳蝈痱弩孱翎糸镱镦翳沆狍蟓骈戾骝镯镱麒殂犰祜黧遽簌盹溟骈汜糸镱麸镱麒殂黠螂忮篝骘箦蜷犰辁狒轱町澡沆狍汜瞌忮盹溟骈邃徭翦骈钺扉狒轱町换泔铙翎铘痫镬泔铘衢铙泔铙翎铘骈钺扉邃镱徜溟糸镱换铒铄邃骘徜溟糸镱犰骈钺扉狒轱箦翩ㄣ灬篌骈戾徙沐篌骒徵沆狍螬磲瓠骒徵ㄣ灬篌骈戾徙沐篌骒徵沆狍螬┅箦翩ㄣ灬篌骈戾篚疱蜚灬篌沆狍螬痫镬徜洵沆狍ㄣ灬篌骈戾泔铙翎铘沆狍螬ㄣ灬篌骈戾篚疱蜚灬篌沆狍螬ㄣ灬篌骈戾沆狍沆狍螬痫镬徜洵沆狍ㄣ灬篌骈戾泔铙翎铘沆狍螬ㄣ灬篌骈戾沆狍沆狍螬┅换ㄦ轭犰辁瀛轭翦蜴徙弩ㄤ镬轶ㄦ殄熹ㄣ灬篌骈戾骈屐潴沆狍螬ㄦ轭犰辁瀛骈屐骈屐沆狍螬ㄤ镬轶礤翳镤ㄣ灬篌骈戾礤翳镤沆狍螬ㄦ轭犰辁瀛礤翳镤礤翳镤沆狍螬换麸瓠戾鲥狒趄殁豸弩铒疳蝈铘狒趄殁豸弩麸蝈驽麸ㄦ轭犰辁瀛狒趄殁豸弩ㄣ灬篌骈戾狒趄殁豸弩沆狍螬铋沆狍螬ㄤ彐躅△蜷翦沆狍蟓骈戾ㄣ灬篌篝蝈犴⒂弪獒扉弩嚆灬篌麸囿趄遽恣徭翦轸栳忮孱骈钺扉邃换桢徜弪黩轸瀛醮ｘ昧婆铝屡篝蝈犴黩轸瀛醪篝蝈犴黩轸瀛醪吹篝蝈犴换泔铙翎铘痫镬黩轸瀛泔铙翎铘ㄣ灬篌骈戾泔铙翎铘沆狍螬篝蝈犴换骒徵黩轸瀛醪ㄣ灬篌骈戾徙沐篌骒徵沆狍螬篝蝈犴换沆狍钺礤黩轸瀛醪ㄣ灬篌骈戾沆狍沆狍螬篝蝈犴换篚疱蜚灬篌黩轸瀛醪ㄣ灬篌骈戾篚疱蜚灬篌沆狍螬篝蝈犴换轭翦蜴徙弩黩轸瀛醪篝蝈犴换骈屐潴黩轸瀛醪戾铉翳ㄣ灬篌骈戾骈屐潴沆狍螬篝蝈犴ㄤ镬轶ㄦ殄熹ㄣ灬篌骈戾骈屐潴沆狍螬ā黩轸瀛骈屐骈屐篝蝈犴┅换礤翳镤黩轸瀛醪戾铉翳ㄣ灬篌骈戾礤翳镤沆狍螬篝蝈犴ㄤ镬轶礤翳镤ㄣ灬篌骈戾礤翳镤沆狍螬ā黩轸瀛礤翳镤礤翳镤篝蝈犴┅换狒趄殁豸弩黩轸瀛狒趄殁豸弩ㄣ灬篌骈戾狒趄殁豸弩沆狍螬篝蝈犴┅ㄤ彐鲠牿憝沆狍蟓溴怩绛痫镬铋⒆桢怙躅麸铒瞽紊鲠祯瀣孱徕戾秕麴豸麸篝犷溽蜾秕麴豸麸犰祜溴怩珑轭秕麴豸镦翳泔铙翎铘箦泗轱镦翳沆狍骈戾ㄤ彐躅黩轸瀛泔铙翎铘ㄣ镱篝犷趔篝蝈犴⒆蜷翦翳泔铙翎铘箦泗轱玳鲥轭嚆镱篝犷趔麸翳沆狍骈戾囿趄遽恣戾è痫镬轭溴癌黩轸瀛醪ū痫镬轭溴泔铙翎铘螬篝蝈犴麒孱牿憝沆狍蟓溴怩绛痫镬簌蠛亥骘蝽狒痫镬泔躅窿ア痫镬轭溴泔铙翎铘螬┅ㄤ镬轶ㄥ铘蝙蝈鲥蝮痫镬孱趄殄蟓扉篝泔铙翎铘螬┅ㄩ钽痫镬轭溴戾è翎ㄣ镱篝犷舡翎孱趄┅麒孱牿憝沆狍蟓溴怩绛痫镬痱轭舡泔铙翎铘孱趄舂黩轸瀛醣翎篝蝈犴ㄣ狍翎ū赵聘黩轸瀛豸娓ㄣ镱篝犷舡豸娓鲠祯孱趄篝蝈犴┅è穿骒镝轭黩轸瀛醮ㄣ镱篝犷舡骒镝舣轭舡鲠祯孱趄篝蝈犴┅è订祜铉滹踱戾黩轸瀛醮祜玑钿ㄡ箬ㄣ镱篝犷舡滹踱戾祜铉鲠祯孱趄巢ｘ破破骀骀篝蝈犴黩轸瀛醮祜玑钿ㄣ镱篝犷舡滹踱戾祜铉鲠祯孱趄ｘ破破骀骀篝蝈犴┅è卑北骈屐潋彐礤翳镤蝈深翦蜴徙逋弭栾潋彐黩轸瀛醪ㄣ镱篝犷舡礤礅弪蝈姝沆狍蟓轭溴孱趄篝蝈犴黩轸瀛醪ㄣ镱篝犷舡礤礅弪蝈姝钺礤豉疱轭溴孱趄篝蝈犴┅ū钺礤令湓疱黩轸瀛醪ㄣ镱篝犷舡钺礤豉疱钺礤轭溴孱趄篝蝈犴黩轸瀛醪ㄣ镱篝犷舡钺礤豉疱溴筱蜷痿矧轭溴孱趄篝蝈犴┅ǚ沆狍黩轸瀛醪ㄣ镱篝犷舡沆狍蟓钺礤轭溴孱趄篝蝈犴┅ǜ篝蜷铉黩轸瀛醪ㄣ镱篝犷舡篝蜷铉鲠祯瀛轭溴孱趄篝蝈犴┅ㄥ蝌矧Ⅶ蜷翦泔铙翎铘痫镬孱趄躅栳钿戾翎宁ア翎绌┅┅┅ㄤ彐躅痱轭舡泔铙翎铘ㄥ铘蝙篝蝈犴⒛邂蹒玳铉桢祓弪麸痱轭翳泔铘孱镦泔铙翎铘痫镬孱趄戾è翎ㄣ镱篝犷舡翎孱趄┅ㄩ钿屮ㄣ镱篝犷舡轭溴孱趄┅簌蠛亥骘蝽狒篝蝈犴痫镬屐屙孱岈翎岈轭溴翎绌ㄣ狍翎ū簌蠛亥骘蝽狒Ⅴ翩负狺アㄣ镱篝犷舡豸娓鲠祯孱趄┅è穿簌蠛亥骘蝽狒㈡楹狺アㄣ镱篝犷舡骒镝舣轭舡鲠祯孱趄┅è订簌蠛亥骘蝽狒浏旌狺アㄣ镱篝犷舡滹踱戾祜铉鲠祯孱趄┅è卑北簌蠛亥骘蝽狒Ⅱ彐岈狺アㄣ镱篝犷舡礤礅弪蝈姝沆狍蟓轭溴孱趄ㄣ镱篝犷舡礤礅弪蝈姝钺礤豉疱轭溴孱趄┅ū簌蠛亥骘蝽狒㈩艉岈狺アㄣ镱篝犷舡钺礤豉疱钺礤轭溴孱趄ㄣ镱篝犷舡钺礤豉疱溴筱蜷痿矧轭溴孱趄┅ǚ簌蠛亥骘蝽狒沆蠛狺アㄣ镱篝犷舡沆狍蟓钺礤轭溴孱趄┅ǜ簌蠛亥骘蝽狒Ⅲ趄狺アㄣ镱篝犷舡篝蜷铉鲠祯瀛轭溴孱趄┅┅┅｜谅锰滹弩瞌躞轭翦蜴徙弩箫滹瞌轫痨屙孱轸桢蝈狒翳轶糸礤ㄤ彐篝蝓泗轭翦蜴徙濠ㄤ彐疳蜥礤翦徙沐篌骒徵蟓磲皤Жê瘐忪殂ｘ鞍氨ê痱轹狒ｘ鞍安ê痱雉邈翦ｘ鞍按ê篝狒殂ｘ鞍案ê骈钺ｘ鞍卑ê鲲灬糸戾ｘ鞍窗ê簌钽栩镱辁邃ｘ鞍舶ê趄犷箝孱ｘ鞍赴ê钺糸鲥ｘ氨鞍ê徕篝蜥泗ｘ按鞍ê篝蜷泗ｘ案鞍┅⑻轶镦脲黠蜾簌礅镬躞邃骘桴磲蝈徜徕戾蝈痱弩孱翎糸镱镦ㄡ沣弩螬骒徵犷翳彘忾钺蝙鲠祯弩ㄤ彐躅磲瓠骒徵ㄦ灬珞⒚犰沲灬翦翳忾繇狃镦翳骒徵骝镯扉篝镦簌礅镬螽蝈漉沐＇灬礅溽祜玳矧矧麒孱礤礅弪ㄣ狎骒徵螬箦泔钿┅癌┅徙沐篌骒徵蟓磲皤洪铋糸犰鲠祯癌ㄤ彐篝蝓泗ㄦ殄熹ê泔铙趄蹉麸ロ犭瀛骈屐洎徙沐篌骒徵钺礤溴筱蜷痿矧狒趄殁豸弩ㄤ彐躅磲脲骈屐钺礤豉疱脲ㄦ灬珞Ж吼踱扉悌┅ē磲脲骈屐横沣弩蟓骒徵骒徵侯犴钺礤轰弩泸轲麸豉疱┅ㄤ彐躅骈屐洵徜洵狒趄殁豸ㄦ殄熹狒趄殁豸濠瘐箬狒趄殁豸ㄦ殄熹狒趄殁豸弩骈屐洎┅ㄤ彐躅骈屐洵狒趄殁豸ㄦ殄熹钺礤ㄦ轭钺礤ㄦ殄熹狒趄殁豸弩骈屐洎呼弩＇篝蜷铉弘妁＇狒趄殁豸瀛钺礤┅ㄤ彐躅骈钺扉瀛骈屐ㄦ殄熹沆狍螬戾è痫镬ㄣ灬篌骈戾泔铙翎铘沆狍螬┅箦翩ㄦ殄熹徙沐篌骒徵骈屐洎磲瓠骒徵ㄦ殄熹徙沐篌骒徵骈屐洎ㄦ殄熹溴筱蜷痿矧骈屐洎痫镬徜洵豸娓痫镬ㄩ铘弪钺飙骈屐洵豉疱ㄦ殄熹溴筱蜷痿矧骈屐洎┅ㄦ殄熹钺礤骈屐洎痫镬徜洵豸娓痫镬ㄦ殄熹钺礤骈屐洎┅ㄦ轭犰辁瀛狒趄殁豸弩ㄦ殄熹狒趄殁豸弩骈屐洎铋沆狍螬ㄤ彐躅△蜷翦骈屐ㄦ殄熹篝蝈犴黩轸瀛醪ㄦ殄熹徙沐篌骒徵骈屐洎篝蝈犴黩轸瀛醪ㄦ殄熹钺礤骈屐洎篝蝈犴黩轸瀛醪ㄦ殄熹溴筱蜷痿矧骈屐洎篝蝈犴黩轸瀛狒趄殁豸弩ㄦ殄熹狒趄殁豸弩骈屐洎篝蝈犴┅ㄤ彐篝蝓泗礤翳镤ê泔铙趄蹉麸ァ磲脲礤翳镤┅徙沐篌骒徵钺礤溴筱蜷痿矧狒趄殁豸弩ㄤ彐躅磲瓠礤翳镤钺礤钺礤⑼弭栾潴箬秕熹忮殇孱糸骈邃怡篝蜷铉泔铘衢铋铉翳彘钺礤蟋矧忮镱镦赭脲黠蜾殇孱糸骈弪麸殇孱糸纟箴邈獒礤翳镤蠛恒灬篌泔铙趄蹉麸恒镱篝蝓泗矧ㄣ镱è羼钺礤恒灬篌泔铙趄蹉麸颟⒓沆轭轸劲è羼钺礤恒镱篝蝓泗矧⒓轭轸劲钺礤┅ㄤ彐躅№犭瀛礤翳镤钺礤蝈趱蝾狎珞脲ㄦ灬珞Ж吼踱扉悌┅ē№犭瀛礤翳镤轰弩泸轲麸ㄣ镱蝈趱蝾狎珞横沣弩蟓骒徵骒徵侯犴钺礤┅ㄤ彐躅礤翳镤徜洵狒趄殁豸礤翳镤狒趄殁豸濠⒘滗噌趑蜷怩翦麸翳扉篝镦狒趄殁豸弩镦囗弭栾洄蝈趱蝾轭噌趑蜷怩翦М瘐箬狒趄殁豸礤翳镤狒趄殁豸弩礤翳镤┅狒趄殁豸濠ㄤ彐躅礤翳镤徜洵泔溴礤翳镤⒚蝈狒弩犷ㄥ眇豉蔑溴狒趄殁豸骘翳礤翳镤蝈趱蝾轭翳泸遽翦狒趄殁豸瀹礤翳镤徜洵狒趄殁豸礤翳镤磲脲泔溴狒趄殁豸ǐ戾铉翳ㄣ潋礤翳镤溴筱蜷痿矧礤翳镤┅ㄩ礤礅弪后翎糸礤翳镤徙沐篌骒徵礤翳镤┅暴┅┅换浇轫痨殂轸翳轶ㄤ彐躅礤翳镤孱篚蝈泔溴礤翳镤⑴铙躜弩翳屮轶翦钽镦蔑溴狒趄殁豸骘翳礤翳镤蝈趱蝾轭翳狒趄殁豸瀹戾è泔溴礤翳镤狒趄殁豸礤翳镤⒚镤澧┅ㄩ铛祆泔溴礤翳镤徜洵泔溴礤翳镤泔溴┅ㄤ彐躅礤翳镤狒趄殁豸礤翳镤钺礤ㄦ轭钺礤礤翳镤狒趄殁豸弩礤翳镤呼弩＇篝蜷铉弘妁＇狒趄殁豸瀛钺礤┅ㄤ彐躅骈钺扉瀛礤翳镤礤翳镤沆狍螬戾è痫镬ㄣ灬篌骈戾泔铙翎铘沆狍螬┅箦翩礤翳镤徙沐篌骒徵礤翳镤磲瓠骒徵礤翳镤徙沐篌骒徵礤翳镤┅礤翳镤溴筱蜷痿矧礤翳镤痫镬徜洵豸娓痫镬ㄡ痧禊＇溴筱蜷痿矧礤翳镤溴筱蜷痿矧礤翳镤┅礤翳镤钺礤礤翳镤痫镬徜洵豸娓痫镬磲瓠礤翳镤钺礤礤翳镤钺礤礤翳镤┅┅ㄦ轭犰辁瀛狒趄殁豸弩礤翳镤狒趄殁豸弩礤翳镤铋沆狍螬ㄤ彐躅△蜷翦礤翳镤礤翳镤篝蝈犴黩轸瀛醪礤翳镤徙沐篌骒徵礤翳镤篝蝈犴黩轸瀛醪礤翳镤钺礤礤翳镤篝蝈犴簌蠛亥骘蝽狒㈨弭栾洵钺礤狺ア礤翳镤钺礤礤翳镤┅黩轸瀛醪礤翳镤溴筱蜷痿矧礤翳镤篝蝈犴黩轸瀛狒趄殁豸弩礤翳镤狒趄殁豸弩礤翳镤篝蝈犴┅ㄤ彐篝蝓泗狒趄殁豸钺礤换铒轭翳沆狍骈戾骈钺扉弪换骢钽糸镱镦狎珲礤铘蠛翳狒趄殁豸瀣疳蝈铘犷沆狍蟓骈戾黩轸弪换骢钽糸镱镦狎珲礤铘蠛翳狒趄殁豸犷翳秕麴豸篝蝈犴ㄤ彐躅骈钺扉瀛狒趄殁豸弩ㄡ趑蜷怩翦狒沆狍螬ㄤ镬轶ㄡ趑蜷怩翦狒趄殁豸弩换狍篚蝈桢徜弪磲脲篚蝈ь犴濮轶轭翳痫镬箦翩ㄡ趑蜷怩翦钺礤狒趄殁豸濠痫镬徜洵豸娓ㄣ灬篌骈戾泔铙翎铘沆狍螬ㄡ趑蜷怩翦钺礤狒趄殁豸濠┅换麇蝈筢鲩铉Ⅱ镲簪狒趄殁豸弩狒趄殁豸弩麒殂栳鲥铒疳蝈铘ㄦ躅汜祆ㄡ趑蜷怩翦骈钺扉弪狒趄殁豸濠狒趄殁豸狒沆狍螬┅ㄤ彐躅黩轸瀛狒趄殁豸弩ㄡ趑蜷怩翦篝蝈犴黩轸瀛醪戾铉翳狒趄殁豸弩篝蝈犴ㄤ镬轶ㄡ趑蜷怩翦狒趄殁豸弩黩轸瀛醪ㄡ趑蜷怩翦钺礤狒趄殁豸濠篝蝈犴换箦躔怩祀汜翥桢骘ㄕ斡汕闻沫沦耘俯换箝钽麇铄邃麸腩秣翳狒趄殁豸戾铉翳ㄥ沆蹁轭翳桢徜弪戾è祜汜飙篝蝈犴簌蠛亥磲脲怡翦狎蜥秕麴豸篝蝈犴┅ㄦ躅汜祆ㄡ趑蜷怩翦黩轸弪狒趄殁豸濠狒趄殁豸祜汜飙篝蝈犴戾è狎蜥簌蠛亥珏舡秕麴豸篝蝈犴狎蜥祜汜飙篝蝈犴┅黩轸瀛醮戾铉翳狎蜥篝蝈犴黩轸瀛箦聃孱沐狎蜥篝蝈犴┅┅ㄤ彐篝蝓泗ㄣ镤瀛狒趄殁豸ê泔钽钺礤泔溴ê轭沆蹁狒趄殁豸钺礤⒚镤澧ㄦ轭犰辁弪＇℃轭犰辁瀛泔溴黩轸弪＇△蜷翦泔溴┅ê泔铙趄蹉麸ロ犭瀛泔溴狒趄殁豸濠磲篝徙磲祜汜祗泔溴屮沐痿轱瞽栳钿戾蝮狒趄殁豸弩换骈屐潴铒轭翳沆狍骈戾篝狎桢蝈换灬忮祗泔铘衢铙镦骟弭轭麸翳泔溴狎蜥徭翦轸骈钺扉邃灬忮祗换犷犰轶沲蝌孱舡祜汜飑换躞邃骘栳钿扉铉铄篝邃咨匀孟呐韵团匀夏忪镢塍ㄤ彐躅泔溴灬忮飙镦骟弭ㄣ镤灬忮飑ㄣ潋ㄡ篌镢灬忮ㄣ镤瀛灬忮祗泔溴┅┅ㄤ彐躅箦翩泔溴灬忮飙镦骟弭镦骟弭泔溴灬忮飑箦翩ㄣ镤瀛灬忮祗泔溴ㄡ泔铙灬忮镦骟弭ㄣ镤瀛灬忮祗泔溴┅┅ㄤ彐躅℃轭犰辁瀛泔溴ㄣ镤疳蝈铘沆狍螬ㄤ邈灬蝈ㄩ珙矧疳蝈铘┅戾è蝈箫祧瀛轭篝蝓泗轱铙ㄣ镥蜚蝈鲥蝮ㄣ镤瀛泔溴泔溴┅鲥泗矧┅┅箦翩ㄣ镤瀛磲篝徙泔溴ㄡ钺禊瀛篝徙悌眭祠轲戾鲠祯瀛忾钿ㄣ灬忮祗ㄣ镤瀛怡翦悌箦翩ㄣ镤瀛泔溴泔溴ㄣ镤瀛灬忮祗泔溴灬忮祗┅ㄤ镬轶ㄥ沐痿轱ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴┅箦翩ㄥ沐痿轱瞽篝狎舡疸屮沐痿轱瞟ㄣ镤瀛灬忮飙镦骟弭泔溴ㄥ沐痿轱瞽篝狎舡疸屮沐痿轱瞟ㄥ沐痿轱瞽孱洵疸屮沐痿轱瞟ㄣ镤瀛灬忮飙镦骟弭泔溴ㄥ沐痿轱瞽孱洵疸屮沐痿轱瞟ㄥ沐痿轱瞽栳钿戾颦疸屮沐痿轱瞟ㄣ镤瀛灬忮飙镦骟弭泔溴ㄥ沐痿轱瞽栳钿戾颦疸屮沐痿轱瞟ㄥ沐痿轱瞽汜翥璀豉疱屮沐痿轱瞟ㄩ铛祆ㄥ沐痿轱瞽汜翥璀豉疱屮沐痿轱瞟换珏铄蜷с狒汨犰歆沆狍轭溴铛礅弪痫镬徜洵沆狍ㄣ灬篌骈戾泔铙翎铘沆狍螬ㄥ沐痿轱瞽汜翥璀豉疱屮沐痿轱瞟┅┅ㄦ轭犰辁瀛狒趄殁豸弩ㄣ镤瀛狒趄殁豸弩泔溴泔溴沆狍螬ㄤ彐躅△蜷翦泔溴ㄣ镤篝蝈犴簌蠛亥骘蝽狒㈨狲篝徙牒狺アㄣ镤瀛磲篝徙泔溴┅黩轸瀛醪ㄣ镤瀛磲篝徙泔溴篝蝈犴簌蠛亥骘蝽狒㈨狲祜汜祗狺アㄣ镤瀛磲祜汜祗泔溴┅黩轸瀛醪ㄣ镤瀛磲祜汜祗泔溴篝蝈犴戾è泔溴狎蜥ㄣ镤瀛泔溴泔溴┅簌蠛亥骘蝽狒㈧孱玺韬狺ア戾铉翳泔溴狎蜥┅黩轸瀛醮戾铉翳泔溴狎蜥篝蝈犴ㄤ雉轫弩ㄩ戾铉翳泔溴狎蜥┅黩轸瀛醣篥蝈泔溴狎蜥椹篝蝈犴┅黩轸瀛醪戾铉翳ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴┅篝蝈犴ㄤ镬轶ㄥ沐痿轱蝈鲥蝮ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴┅簌蠛亥骘蝽狒Ⅲ翎螋疸狺アㄥ沐痿轱瞽篝狎舡疸屮沐痿轱瞟黩轸瀛醪ㄥ沐痿轱瞽篝狎舡疸屮沐痿轱瞟篝蝈犴簌蠛亥骘蝽狒㈠钿疸狺アㄥ沐痿轱瞽孱洵疸屮沐痿轱瞟黩轸瀛醪ㄥ沐痿轱瞽孱洵疸屮沐痿轱瞟篝蝈犴簌蠛亥骘蝽狒㈣犷潇弪疸狺アㄥ沐痿轱瞽栳钿戾颦疸屮沐痿轱瞟黩轸瀛醪ㄥ沐痿轱瞽栳钿戾颦疸屮沐痿轱瞟篝蝈犴黩轸瀛醪ㄥ沐痿轱瞽汜翥璀豉疱屮沐痿轱瞟篝蝈犴┅黩轸瀛狒趄殁豸弩ㄣ镤瀛狒趄殁豸弩泔溴篝蝈犴┅ㄤ彐躅磲脲泔溴狒趄殁豸ㄡ蜱泔躅舂⒚蝈狒弩犷屙痿蔑溴狒趄殁豸骘礤翳镤麒殂翎脲噌蜱泔躅羿疳蜥礤翦蝮轭沆蹁轭翳轫痨殂轸圄栝筻疳蜥礤翦虍ē磲脲泔溴狒趄殁豸喉狲祜汜祗狎绛泔躅舂ㄤ彐躅泔溴徜洵狒趄殁豸ㄣ镤狒趄殁豸濠⒘滗噌趑蜷怩翦麸嚆镤濮蝈趱蝾轭噌趑蜷怩翦М瘐箬狒趄殁豸ㄣ镤瀛狒趄殁豸弩泔溴┅狒趄殁豸濠ㄤ彐躅泔溴狒趄殁豸ㄣ镤钺礤ㄦ轭钺礤ㄣ镤瀛狒趄殁豸弩泔溴呼弩＇篝蜷铉弘妁＇狒趄殁豸瀛钺礤┅ㄤ彐躅泔溴徜洵屮沐痿轱瞽栳钿戾ㄣ镤篝狎孱栳钿戾豉疱瘐箬磲脲屮沐痿轱后翎螋疸篝狎哄钿疸孱鸿犷潇弪疸栳钿戾恒狒汨豉疱豉疱ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴┅ㄤ彐躅徜洵屮沐痿轱瞽栳钿戾篝狎孱栳钿戾豉疱ㄣ镤瀛徜洵屮沐痿轱瞽栳钿戾沲蝌孱舡泔溴狒趄殁豸濯篝狎孱栳钿戾豉疱┅ㄤ彐篝蝓泗屮沐痿轱篝狎舡疸换灬忮翎蜱弭孱洵疸换灬忮翎蜱弭栳钿戾颦疸换灬忮翎蜱弭汜翥璀豉疱换篝蜷铉骘箴邈殒殂豉疱矧紊骘犰ㄤ彐鲠沲蝌孱舡泔溴狒趄殁豸濯铋飑ㄤ彐躅筢鲥泔溴箴邈獒祗ㄣ镤濠箦翩ㄣ镤瀛泔溴泔溴泔溴ㄣ镤瀛磲祜汜祗泔溴蝈玳篝弪蟓犰祜汜翦洫换ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴栳钿戾蝮ㄣ镤瀛沲蝌孱舡祜汜泔溴蝈玳篝弪┅ㄤ彐躅蝈篝矧瀛泔溴箴邈獒祗ㄣ镤濠箦翩泔溴ㄣ镤瀛泔溴泔溴换栳钿戾蝮ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮泔溴蝈玳篝弪蟓犰祜汜翦洫ㄣ镤瀛磲祜汜祗泔溴蝈玳篝弪ㄣ镤瀛沲蝌孱舡祜汜泔溴┅ㄤ彐磲泸鏖翳泔溴麸礤翳镤è沆狍蟓骈戾礤翳镤脲筢驽铄篝轭绌怙澌怙澌戾èㄧ孱簌愆ㄣㄧ孱簌愆┅啜痱镧括麒孱筢驽铄篝轭啜麒孱沲蝌孱舡泔溴狒趄殁豸濯筢鲥泔溴箴邈獒祗沲蝌孱舡泔溴狒趄殁豸濯┅┅戾舄è礤翳镤ì礤翳镤孱篚蝈泔溴礤翳镤┅í痫镬ㄣ灬篌骈戾泔铙翎铘沆狍蟓骈戾┅í泔溴ㄣ镤瀛泔溴悌í蝈玳篝弪蟓犰祜汜翦洫ㄣ镤瀛磲祜汜祗悌í蝈玳篝弪ㄣ镤瀛沲蝌孱舡祜汜悌í沲蝌孱舡泔溴狒趄殁豸濯悌棱镤箦翩ㄣ镤瀛泔溴悌泔溴换ㄣ镤瀛屮沐痿轱瞽栳钿戾蝮悌栳钿戾蝮ㄣ镤瀛磲祜汜祗悌蝈玳篝弪蟓犰祜汜翦洫┅括麒孱筢驽铄篝轭啜麒孱沲蝌孱舡泔溴狒趄殁豸濯蝈篝矧瀛泔溴箴邈獒祗沲蝌孱舡泔溴狒趄殁豸濯┅┅┅ㄤ彐篝蝓泗箫躜沐骈戾狒趄殁豸ê泔钽钺礤箫躜沐ê轭沆蹁狒趄殁豸钺礤⒂秕蜚迤殪澧┅骈戾钺礤ㄤ彐篝蝓泗扉铄铛礅弪蟓狒趄殁豸ê轭沆蹁狒趄殁豸钺礤⑻轭逦蹴忮蛟徕戾┅扉铄铛礅弪螬ㄤ彐篝蝓泗扉铄铛礅弪篝狎舡疸扉铄ㄤ彐篝蝓泗祜汜飙鲠蜷徕戾蟓狒趄殁豸ê泔钽钺礤祜汜飙鲠颦ê轭沆蹁狒趄殁豸钺礤⑻镢犰轴蜷徕戾葬忪澧┅祜汜祗ㄤ彐篝蝓泗祜汜飙鲠蜷徕戾ê泔钽钺礤祜汜飙┅篝狎舡疸戾铉翳钺礤溴筱蜷痿矧轭溴｜换翳轶轶翳黹铋磲箦聃孱沐麇铄邃麸篚痧矧艉换泸遽翦沆狍骈戾篝蝓泗躜换徜礤翳镤换徜泔溴麸翳礤翳镤蟋篦轸汨轭骝镯镱礤翳镤麸翳雉桢换骈钺扉翳礤翳镤蟋镱怡镱换黩轸翳沆狍骈戾麸篚痧矧翳箦聃孱沐徕秭瀣麇痱镡徕禊铄邃麸忮徕戾麸骈钿礤翳镤怡箝珙狒躜骈钿翳礤翳镤泔溴狒趄殁豸徜泔溴麸翳泔溴狒趄殁豸骈钺扉翳泔溴狒趄殁豸泔铘孱趔ㄢ祜汶轭轸骘骢螋桢徜溟糸镱